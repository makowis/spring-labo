plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.flywaydb.flyway" version "8.5.9"
	id 'java'
}

group = 'com.makowis'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

flyway {
	url = "${getProperty("datasource.url")}"
	user = "${getProperty("datasource.user")}"
	password = "${getProperty("datasource.password")}"
}

test {
	useJUnitPlatform()

	testLogging {
		// イベントを出力する (TestLogEvent)
		events 'started', 'skipped', 'passed', 'failed'
		// 例外発生時の出力設定 (TestExceptionFormat)
		exceptionFormat 'full'
	}

	afterSuite { desc, result ->
		if (!desc.parent) {
			println "\nTest result: ${result.resultType}"
			println "Test summary: ${result.testCount} tests, " +
					"${result.successfulTestCount} succeeded, " +
					"${result.failedTestCount} failed, " +
					"${result.skippedTestCount} skipped"
		}
	}
}